plugins {
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.2.1'
    id 'signing'
    id 'maven'
}
apply from: 'gradle/mavenPublish.gradle'

group = 'com.gluonhq'
version = '0.0.5-SNAPSHOT'

mainClassName = 'com.gluonhq.strange.demo.Demo'

repositories {
    mavenCentral()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
}

def currentOS = org.gradle.internal.os.OperatingSystem.current();
def javafxclassifier
if (currentOS.isWindows()) {
    javafxclassifier = 'win'
} else if (currentOS.isLinux()) {
    javafxclassifier = 'linux'
} else if (currentOS.isMacOsX()) {
    javafxclassifier = 'mac'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

dependencies {
    // compile "com.gluonhq:charm-cloudlink-client:4.4.7"
    // runtime "com.gluonhq:charm-down-plugin-storage-desktop:3.6.0"
    compile "org.openjfx:javafx-controls:11:$javafxclassifier"
    compile "org.openjfx:javafx-graphics:11:$javafxclassifier"
    compile "org.openjfx:javafx-base:11:$javafxclassifier"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

ext.publish = !project.version.endsWith('SNAPSHOT')

configurations {
    published
}


artifacts {
    archives sourceJar, javadocJar
}

